%scanner                XyzScanner.h
%scanner-token-function d_scanner.lex()

%token INT FLOAT WORD
%token NEWLINE
%token ATOM_COUNT_INT
%polymorphic INT_TYPE: int; FLOAT_TYPE: double; STRING_TYPE: std::string;
%type <INT_TYPE> atom_count_integer
%type <FLOAT_TYPE> real
%type <STRING_TYPE> word

%%

snapshots:
  // empty
|
  snapshots
  snapshot
;

snapshot:
  atom_count_integer NEWLINE
  {
    addSnapshot($1);
  }
  comment NEWLINE
  atoms
;

atoms:
  // empty
|
  atoms
  atom NEWLINE
;

atom:
  //   X    Y    Z
  word real real real
  {
    addAtom($1, $2, $3, $4);
  }
;

atom_count_integer:
  ATOM_COUNT_INT
  {
    $$ = stoi(d_scanner.matched());
  }
;

real:
  FLOAT
  {
    $$ = stod(d_scanner.matched());
  }
|
  INT
  {
    $$ = stod(d_scanner.matched());
  }
;

word:
  WORD
  {
    $$ = d_scanner.matched();
  }
|
  FLOAT
  {
    $$ = d_scanner.matched();
  }
|
  INT
  {
    $$ = d_scanner.matched();
  }
;

comment:
  // empty
|
  comment word
  {
    addCommentWord($2);
  }
;